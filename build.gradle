// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.jetbrains.kotlin.android" version "1.4.31" apply false
    id "org.jetbrains.kotlin.kapt" version "1.4.31" apply false
    id "com.github.ben-manes.versions" version "0.38.0"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr'].any { qualifier ->
                selection.candidate.version.contains(qualifier)
            }

            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()

        // maven { url 'https://kotlin.bintray.com/kotlinx' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all { task ->
        sourceCompatibility = JavaVersion.VERSION_1_6
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"

            useIR = true

            freeCompilerArgs += [
                    "-Xuse-experimental=kotlin.contracts.ExperimentalContracts",
                    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
                    "-Xuse-experimental=kotlin.time.ExperimentalTime",
                    "-XXLanguage:+InlineClasses",
            ]

            if (task.name == "compileReleaseKotlin") {
                freeCompilerArgs += [
                        "-Xno-call-assertions",
                        "-Xno-param-assertions",
                        "-Xno-receiver-assertions",
                ]
            }
        }
    }
}
