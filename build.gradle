// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.2'
    }
}

plugins {
    id "org.jetbrains.kotlin.android" version "1.9.23" apply false
    id "org.jetbrains.kotlin.kapt" version "1.9.23" apply false
    id "com.github.ben-manes.versions" version "0.51.0"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', '-m'].any { qualifier ->
                selection.candidate.version.toLowerCase().contains(qualifier)
            }

            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all { task ->
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        kotlinOptions {
            jvmTarget = "11"

            freeCompilerArgs += [
                    "-opt-in=kotlin.ExperimentalStdlibApi",
                    "-opt-in=kotlin.contracts.ExperimentalContracts",
                    "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                    "-opt-in=kotlinx.coroutines.FlowPreview",
                    "-opt-in=kotlin.time.ExperimentalTime",
            ]

            if (task.name == "compileReleaseKotlin") {
                freeCompilerArgs += [
                        "-Xno-call-assertions",
                        "-Xno-param-assertions",
                        "-Xno-receiver-assertions",
                ]
            }
        }
    }
}
